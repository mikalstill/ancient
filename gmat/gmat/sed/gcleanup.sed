s/\\f(C\([BIW]\)/\\\&\\f(C\1/g
s/\\f\[CRN\]/\\\&\\f\[CRN\]/g
s/\\f\[CNI\]/\\\&\\f\[CNI\]/g
s/\\f\[CBO\]/\\\&\\f\[CBO\]/g
/^\.[IOH]L/s/\\\\/\\e/g
/^\.FAK/s/\\\\/\\e/g
/^\.CO/s/\\\\/\\e/g
/^\.syn/s/\\\\/\\e/g
/^\.Ps/,/^\.Pe/b
/^\.Xs/,/^\.Xe/b
/^\.PS/,/^\.PE/b
/^\.Fs/,/^\.Fe/b
/^\.ES/,/^\.EE/b
/^\.G1/,/^\.G2/b
/^\.EQ/,/^\.EN/b
/^\.SCR/,/^\.\/SCR/b
/^\.SYN/,/^\.\/SYN/b
/^\.LLP/,/^\.\/LLP/b
/^\.BSCR/,/^\.\/BSCR/b
s/[     ][      ]*$//
s/\.\.\.\./\\h'0.4n'.\\h'0.4n'.\\h'0.4n'.\\h'0.4n'.\\h'0.4n'/g
s/\.\.\./\\h'0.4n'.\\h'0.4n'.\\h'0.4n'.\\h'0.4n'/g
/---/!{
        s/--/\\(em/g
}
/^\.[ABC]h /{
	s;\([0-9]*\) "\(.*\)" "\(.*\)";\1 !#!\2!#! !#!\3!#!;
: doquota
	/!#!/s;";\\(lq;
	/!#!/s;";\\(rq;
	t doquota
}
/^\.[ABC]h /{
	s;\([0-9]*\) "\(.*\)";\1 !#!\2!#!;
: doquotb
	/!#!/s;";\\(lq;
	/!#!/s;";\\(rq;
	t doquotb
}
/^\.[ABC]h "/{
	s;"\(.*\)" "\(.*\)";!#!\1!#! !#!\2!#!;
: doquotc
	/!#!/s;";\\(lq;
	/!#!/s;";\\(rq;
	t doquotc
}
/^\.[ABC]h "/{
	s;"\(.*\)";@#@\1@#@;
: doquotd
	/@#@/s;";\\(lq;
	/@#@/s;";\\(rq;
	t doquotd
}
/^\.[ABC]h/{
s;@#@;";g
s;!#!;";g
}
/^[.']/b
/^\\!\./b
/^%/s/"/<">/g
s/\\".*$//
s/^"/\\(lq/
s/"$/\\(rq/
s/"? /\\(rq? /g
s/"?$/\\(rq?/g
s/ "/ \\(lq/g
s/" /\\(rq /g
s/      "/      \\(lq/g
s/"     /\\(rq     /g
s/")/\\(rq)/g
s/"]/\\(rq]/g
s/("/(\\(lq/g
s/\["/\[\\(lq/g
s/";/\\(rq;/g
s/":/\\(rq:/g
s/,"/,\\(rq/g
s/",/\\(rq,/g
s/"-/\\(rq-/g
s/\."/.\\\&\\(rq/g
s/"\./\\(rq.\\\&/g
s/"\\^\\(em/\\(rq\\(em/g
s/\\(em\\^"/\\(em\\(lq/g
s/"\\(em/\\(rq\\(em/g
s/\\(em"/\\(em\\(lq/g
s/@"/@\\(lq/
s/	"/	\\(lq/
s/"@/\\(rq@/g
s/"	/\\(rq	/g
s/<\\(lq>/"/g
s/<\\(rq>/"/g
s/<">/"/g
s/<">/"/g
s/@DQ@/"/g
/\$.*\$/s/\\([rl]q/"/g
