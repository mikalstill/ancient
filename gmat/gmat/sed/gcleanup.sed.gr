s/\\f(C\([BIW]\)/\\\&\\f(C\1/g
s/\\f\[CRN\]/\\\&\\f\[CRN\]/g
s/\\f\[CNI\]/\\\&\\f\[CNI\]/g
s/\\f\[CBO\]/\\\&\\f\[CBO\]/g
/^\.Ps/,/^\.Pe/b
/^\.Xs/,/^\.Xe/b
/^\.PS/,/^\.PE/b
/^\.Fs/,/^\.Fe/b
/^\.ES/,/^\.EE/b
/^\.G1/,/^\.G2/b
/^\.EQ/,/^\.EN/b
s/[     ][      ]*$//
s/\.\.\.\./\\h'0.4n'.\\h'0.4n'.\\h'0.4n'.\\h'0.4n'.\\h'0.4n'/g
s/\.\.\./\\h'0.4n'.\\h'0.4n'.\\h'0.4n'.\\h'0.4n'/g
/---/!{
        s/--/\\|\\(em\\|/g
}
/^\.TS/,/^\.TE/b
/^\.[ABC]h /{
	s;\([0-9]*\) "\(.*\)" "\(.*\)";\1 !#!\2!#! !#!\3!#!;
: doquota
	/!#!/s;";\\[Fc];
	/!#!/s;";\\[Fo];
	t doquota
}
/^\.[ABC]h /{
	s;\([0-9]*\) "\(.*\)";\1 !#!\2!#!;
: doquotb
	/!#!/s;";\\[Fc];
	/!#!/s;";\\[Fo];
	t doquotb
}
/^\.[ABC]h "/{
	s;"\(.*\)" "\(.*\)";!#!\1!#! !#!\2!#!;
: doquotc
	/!#!/s;";\\[Fc];
	/!#!/s;";\\[Fo];
	t doquotc
}
/^\.[ABC]h "/{
	s;"\(.*\)";@#@\1@#@;
: doquotd
	/@#@/s;";\\[Fc];
	/@#@/s;";\\[Fo];
	t doquotd
}
/^\.[ABC]h/{
s;@#@;";g
s;!#!;";g
}
/^[.']/b
/^\\!\./b
/^%/s/"/<">/g
s/\\".*$//
s/^"/\\[Fc]/
s/"$/\\[Fo]/
s/"? /\\[Fo]? /g
s/"?$/\\[Fo]?/g
s/ "/ \\[Fc]/g
s/" /\\[Fo] /g
s/      "/      \\[Fc]/g
s/"     /\\[Fo]     /g
s/")/\\[Fo])/g
s/"]/\\[Fo]]/g
s/("/(\\[Fc]/g
s/\["/\[\\[Fc]/g
s/";/\\[Fo];/g
s/":/\\[Fo]:/g
s/,"/,\\[Fo]/g
s/",/\\[Fo],/g
s/"-/\\[Fo]-/g
s/\."/.\\\&\\[Fo]/g
s/"\./\\[Fo].\\\&/g
s/"\\|\\(em/\\[Fo]\\(em/g
s/\\(em\\|"/\\(em\\[Fc]/g
s/"\\(em/\\[Fo]\\(em/g
s/\\(em"/\\(em\\[Fc]/g
s/<\\[Fc]>/"/g
s/<\\[Fo]>/"/g
s/<">/"/g
s/<">/"/g
